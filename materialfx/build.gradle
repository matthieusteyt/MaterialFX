import org.apache.tools.ant.taskdefs.condition.Os

plugins {
    id 'java-library'
    id 'biz.aQute.bnd.builder' version '6.2.0'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

group 'io.github.palexdev'
version = materialfx_version

repositories {
    mavenCentral()

    flatDir {
        dirs "${project(':demo').projectDir}/libs"
    }
}

compileJava {
    sourceCompatibility = '11'
    targetCompatibility = '11'
}

dependencies {
    testImplementation('junit:junit:4.13.2')

    api 'io.github.palexdev:virtualizedfx:11.2.6'
}

javafx {
    configuration = 'compileOnly'
}

javadoc {
    excludes = ['**/*.html', 'META-INF/**']

    options.use = true
    options.splitIndex = true
    options.encoding = 'UTF-8'
    options.author = true
    options.version = true
    options.windowTitle = "$project.name $project.version API"
    options.docTitle = "$project.name $project.version API"
    options.links = ['https://docs.oracle.com/en/java/javase/11/docs/api',
                     'https://openjfx.io/javadoc/17']
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set('javadoc')
    from javadoc.destinationDir
}

task sourcesJarBuild(type: Jar, dependsOn: classes) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar
    archives sourcesJarBuild
    archives jar
}

jar {
    manifest {
        attributes(
                'Bundle-Name': project.name,
                'Bundle-Description': "Material controls for JavaFX",
                'Bundle-SymbolicName': 'io.github.palexdev',
                'Export-Package': 'io.github.palexdev.materialfx.*, io.github.palexdev.materialfx.demo.*'
        )
    }
}

shadowJar {
    mergeServiceFiles()
    dependencies {
        include(dependency('io.github.palexdev:virtualizedfx:11.2.6'))
    }
}

task copyJar(type: Copy) {
    from jar
    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        into System.getenv("APPDATA") + '/Scene Builder/Library'
    } else if (Os.isFamily(Os.FAMILY_MAC)) {
        into System.getProperty("user.home") + '/Library/Application Support' + '/Scene Builder/Library'
    } else if (Os.isFamily(Os.FAMILY_UNIX)) {
        into System.getProperty("user.home") + '/.scenebuilder/Library'
    }
}

task removeBnd(type: Delete) {
    delete fileTree(project.buildDir) {
        include '**/*.bnd'
    }
}

build {
    dependsOn shadowJar, copyJar, removeBnd
}

//mavenPublish {
//    sonatypeHost = "S01"
//}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            afterEvaluate {
                artifact sourcesJarBuild
                artifact javadocJar
            }

            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }

            pom {
                name = 'materialfx'
                description = 'Material Desgin components for JavaFX'
                url = 'https://github.com/palexdev/MaterialFX'
                licenses {
                    license {
                        name = 'GNU LGPLv3'
                        url = 'https://www.gnu.org/licenses/gpl-3.0.html'
                        distribution = 'repo'
                    }
                }
                developers {
                    developer {
                        name = 'palexdev'
                        email = 'Alessandro Parisi'
                        url= 'https://github.com/palexdev'
                        roles = [
                                'author',
                                'developer'
                        ]
                    }
                }
            }
        }
    }
}

//GROUP=io.github.palexdev
//POM_ARTIFACT_ID=materialfx
//VERSION_NAME=11.13.5
//
//POM_NAME=materialfx
//POM_DESCRIPTION=Material Desgin components for JavaFX
//POM_INCEPTION_YEAR=2020
//
//POM_URL=https://github.com/palexdev/MaterialFX
//POM_SCM_URL=https://github.com/palexdev/MaterialFX
//
//POM_LICENCE_NAME=GNU LGPLv3
//POM_LICENCE_URL=https://www.gnu.org/licenses/gpl-3.0.html
//POM_LICENCE_DIST=repo
//
//POM_DEVELOPER_ID=palexdev
//POM_DEVELOPER_NAME=Alessandro Parisi
//POM_DEVELOPER_URL=https://github.com/palexdev
//
//POM_PACKAGING=jar

